Multichar_Symbols

!! Below are the multichar symbols relevant to Michif verbal morphology

[VAI]
[VTI]
[VTA]

[DIR]
[INV]

[OBV]
[PRS]
[FUT]
[PST]
[INCL]
[EXCL]

[1] !! DELETE ME?
[1SG]
[2SG]
[3SG]
[NON3SG]
[1PL]
[3PL]
[3-SUBJ]

[1-OBJ]
[1SG-OBJ]
[1PL-OBJ]
[3-OBJ]
[3SG-OBJ]
[3PL-OBJ]

!! Flags for Michif verbal morphology

@P.order.indep@
@R.order.indep@
@D.order.indep@
@P.order.conj@
@R.order.conj@
@P.person.ni@
@R.person.ni@
@P.person.ki@
@R.person.ki@
@D.person@
@P.tense.kii@
@R.tense.kii@
@P.tense.ka@
@R.tense.ka@
@P.tense.ka@
@D.tense@
@P.obv.yi@
@R.obv.yi@
@P.obv.a@
@R.obv.a@
@R.obv@
@D.obv@
@P.direction.aa@
@R.direction.aa@
@P.direction.ikw@
@R.direction.ikw@
@P.direction.ee@
@R.direction.ee@
@P.direction.i@
@R.direction.i@
@P.direction.it@
@R.direction.it@

LEXICON Root

0 Verb ;


LEXICON Verb

@P.order.indep@   TenseMarking ;
@P.order.indep@@P.person.ni@:@P.order.indep@@P.person.ni@ni%< TenseMarking ;
@P.order.indep@@P.person.ki@:@P.order.indep@@P.person.ki@ki%< TenseMarking ;

@P.order.conj@:@P.order.conj@ee-%< TenseMarking ; ! TODO: Ask Heather about hyphen

LEXICON TenseMarking

0 Preverb ; ! No tense marking
@P.tense.kii@:@P.tense.kii@kii%< Preverb ; 
@P.tense.ka@:@P.tense.ka@ka%< Preverb ;

LEXICON Preverb

0 VerbStem ; ! No preverb
nohtee%< VerbStem ; ! ('want')
machii%< VerbStem ; ! ('begin')

LEXICON VerbStem

0 VAIStem ;
0 VTIStem ;
0 VTAStem ;

LEXICON VAIStem

!ayaw:ay        VAIInflection; ! 'be there', 'live there', stay there'
itohteew:itoht    VAIInflection; ! 'go', 'go there'
!shipweeteew:shipweet VAIInflection; ! 'leave', 'depart'
!itweew:it VAIInflection; ! 'say'
!kiweew:kiw      VAIInflection; ! 'return home'
!meekiw:meekiw     VAIInflection; ! 'give'

LEXICON VTIStem

daweeyihtam:daweeyiht VTIInflection ; ! 'want', 'desire'
!tootam:toot VTIInflection ; ! 'do'
!miyeutam:miyeut VTIInflection ; ! 'like', 'be glad about', 'think well of'
!meekiw VTIInflection ; ! 'give'
!kishkeetam:kishkeet VTIInflection ; ! 'know'
!waapahtam:waapaht VTIInflection ; ! 'see'

LEXICON VTAStem

!ayaaweew:ay VTAInflection ; ! 'own'
!itweew:itw VTAInflection ; ! 'say this to'
waapameew:waapam VTAInflection ; ! 'see', 'witness'
!miyew:miy VTAInflection ; ! 'give'
!miyeumew:miyeum VTAInflection ; ! 'like', 'think well of', 'be pleased with'
!daweeyimew:daweeyim VTAInflection ; ! 'want', 'desire', 'desire from'
!kishkeemew:kishkeem VTAInflection ; ! 'know', 'know about'

LEXICON VAIInflection

[VAI]:%> ObviativeMarking ;

LEXICON VTIInflection 

[VTI]:%> VTIOrder ;

LEXICON VTAInflection

[VTA]:%> DirectionMarking ;

LEXICON ObviativeMarking !! Accepts for the first obviation marking

0 VAIOrder ; ! No obviative marking
@P.obv.yi@:@P.obv.yi@yi%> VAIOrder;

LEXICON DirectionMarking

@P.direction.aa@:@P.direction.aa@aa%> VTAOrder ;
@P.direction.ikw@:@P.direction.ikw@ikw%> VTAOrder ;
@P.direction.ee@:@P.direction.ee@ee%> VTAOrder ;
@P.direction.i@:@P.direction.i@i%> VTAOrder ;
@P.direction.it@:@P.direction.it@it%> VTAOrder ;

LEXICON VAIOrder !! Outputs the order tag
@R.order.indep@[IND]:@R.order.indep@ VAITense ;
@R.order.conj@[CONJ]:@R.order.conj@ VAITense ;

LEXICON VTIOrder !! Outputs the order tag
@R.order.indep@[IND]:@R.order.indep@ VTITense ;
@R.order.conj@[CONJ]:@R.order.conj@ VTITense ;

LEXICON VTAOrder

@R.order.indep@[IND]:@R.order.indep@ VTATense ;
@R.order.conj@[CONJ]:@R.order.conj@ VTATense ;

LEXICON VAITense   !! Outputs the tense tag
@D.tense@[PRS]:@D.tense@ VAIPersonMarking ;
@R.tense.kii@[PST]:@R.tense.kii@ VAIPersonMarking ;
@R.tense.ka@[FUT]:@R.tense.ka@ VAIPersonMarking ;

LEXICON VTITense   !! Outputs the tense tag
@D.tense@[PRS]:@D.tense@ VTIPersonMarking ;
@R.tense.kii@[PST]:@R.tense.kii@ VTIPersonMarking ;
@R.tense.ka@[FUT]:@R.tense.ka@ VTIPersonMarking ;

LEXICON VTATense

@D.tense@[PRS]:@D.tense@ VTAPersonMarking ;
@R.tense.kii@[PST]:@R.tense.kii@ VTAPersonMarking ;
@R.tense.ka@[FUT]:@R.tense.ka@ VTAPersonMarking;

LEXICON VAIPersonMarking

@R.order.indep@@R.person.ni@[1SG]:@R.order.indep@@R.person.ni@aan VAIObviative1 ;
@R.order.indep@@R.person.ki@[2SG]:@R.order.indep@@R.person.ki@aan VAIObviative1 ;
@R.order.indep@@D.person@[3SG]:@R.order.indep@@D.person@eew VAIObviative1 ;
@R.order.indep@@D.person@[NON3SG]:@R.order.indep@@D.person@een VAIObviative1 ;
@R.order.indep@@R.person.ni@[1PL][EXCL]:@R.order.indep@eenaan VAIObviative1 ;
@R.order.indep@@R.person.ki@[1PL][INCL]:@R.order.indep@eenaan VAIObviative1 ;
@R.order.indep@@R.person.ki@[2PL][EXCL]:@R.order.indep@@R.person.ki@eenaawaaw VAIObviative1 ;
@R.order.indep@@R.person.ki@[2PL][INCL]:@R.order.indep@@R.person.ki@eenaan VAIObviative1 ;
@R.order.indep@@D.person@[3PL]:@R.order.indep@@D.person@eewak VAIObviative1 ;

@R.order.conj@[1SG]:@R.order.conj@eeyaan VAIObviative1 ;
@R.order.conj@[2SG]:@R.order.conj@eeyan VAIObviative1 ;
@R.order.conj@[3SG]:@R.order.conj@eet VAIObviative1 ;
@R.order.conj@[1PL][EXCL]:@R.order.conj@eeyaahk VAIObviative1 ;
@R.order.conj@[1PL][INCL]:@R.order.conj@eeyahk VAIObviative1 ;
@R.order.conj@[2PL][EXCL]:@R.order.conj@eeyehk VAIObviative1 ;
@R.order.conj@[2PL][INCL]:@R.order.conj@eeyahk VAIObviative1 ;
@R.order.conj@@D.person@[3PL]:@R.order.conj@eechik VAIObviative1 ;
@R.order.conj@@D.person@[3PL]:@R.order.conj@eetik VAIObviative1 ;

LEXICON VTIPersonMarking 
@R.order.indep@@R.person.ni@[1SG][3-OBJ]:@R.order.indep@@R.person.ni@een EndVerb ;
@R.order.indep@@R.person.ki@[2SG][3-OBJ]:@R.order.indep@@R.person.ki@een EndVerb ;
@R.order.indep@@D.person@[3SG][3-OBJ]:@R.order.indep@@D.person@am EndVerb ;
@R.order.indep@@R.person.ni@[1PL][3-OBJ][EXCL]:@R.order.indep@@R.person.ni@enaan EndVerb ;
@R.order.indep@@R.person.ki@[1PL][3-OBJ][INCL]:@R.order.indep@@R.person.ki@enaan EndVerb ;
@R.order.indep@@R.person.ni@[2PL][3-OBJ][EXCL]:@R.order.indep@@R.person.ki@eenawaw EndVerb ;
@R.order.indep@@R.person.ki@[2PL][3-OBJ][INCL]:@R.order.indep@@R.person.ki@eenan EndVerb ;
@R.order.indep@@D.person@[3PL][3-OBJ]:@R.order.indep@@D.person@amwak EndVerb ;

@R.order.conj@[1SG][3-OBJ]:@R.order.conj@aman EndVerb ;
@R.order.conj@[2SG][3-OBJ]:@R.order.conj@amen EndVerb ;
@R.order.conj@[3SG][3-OBJ]:@R.order.conj@ak EndVerb ;
@R.order.conj@[1PL][3-OBJ][EXCL]:@R.order.conj@amaahk EndVerb ;
@R.order.conj@[1PL][3-OBJ][INCL]:@R.order.conj@amahk EndVerb ;
@R.order.conj@[2PL][3-OBJ][EXCL]:@R.order.conj@amehk EndVerb;
@R.order.conj@[2PL][3-OBJ][INCL]:@R.order.conj@eamahk EndVerb ;
@R.order.conj@[3PL][3-OBJ]:@R.order.conj@akik EndVerb ;

LEXICON VTAPersonMarking

!! First Person Subject Independent Order Direct
@R.order.indep@@R.person.ni@@R.direction.aa@[1SG][DIR][3SG-OBJ]:@R.order.indep@@R.person.ni@@R.direction.aa@w EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.aa@[1SG][DIR][3PL-OBJ]:@R.order.indep@@R.person.ni@@R.direction.aa@waak EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.aa@[1PL][DIR][3SG-OBJ][EXCL]:@R.order.indep@@R.person.ki@@R.direction.aa@nan EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.aa@[1PL][DIR][3PL-OBJ][EXCL]:@R.order.indep@@R.person.ki@@R.direction.aa@naanik EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[1PL][DIR][3SG-OBJ][INCL]:@R.order.indep@@R.person.ni@@R.direction.aa@naan EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[1PL][DIR][3PL-OBJ][INCL]:@R.order.indep@@R.person.ni@@R.direction.aa@naanik EndVerb ;

!! Independent Order Inverse
@R.order.indep@@R.person.ni@@R.direction.ikw@[3SG][INV][1SG-OBJ]:@R.order.indep@@R.person.ni@@R.direction.ikw@ EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.ikw@[3PL][INV][1SG-OBJ]:@R.order.indep@@R.person.ni@@R.direction.ikw@aak EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.ikw@[3SG][INV][1PL-OBJ][EXCL]:@R.order.indep@@R.person.ki@@R.direction.ikw@naan EndVerb ;
@R.order.indep@@R.person.ni@@R.direction.ikw@[3PL][INV][1PL-OBJ][EXCL]:@R.order.indep@@R.person.ki@@R.direction.ikw@naanik EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3SG][INV][1PL-OBJ][INCL]:@R.order.indep@@R.person.ni@@R.direction.ikw@naan EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3PL][INV][1PL-OBJ][INCL]:@R.order.indep@@R.person.ni@@R.direction.ikw@naanik EndVerb ;

!! First Person Subject Conjunct Order Direct
@R.order.conj@@R.person.ni@@R.direction.aa@[1SG][DIR][3SG-OBJ]:@R.order.conj@@R.person.ni@@R.direction.aa@k EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.aa@[1SG][DIR][3PL-OBJ]:@R.order.conj@@R.person.ni@@R.direction.aa@kik EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.aa@[1PL][DIR][3SG-OBJ][EXCL]:@R.order.conj@@R.person.ni@@R.direction.aa@yaahk EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.aa@[1PL][DIR][3PL-OBJ][EXCL]:@R.order.conj@@R.person.ni@@R.direction.aa@yaahkwik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[1PL][DIR][3SG-OBJ][INCL]:@R.order.conj@@R.person.ki@@R.direction.aa@yaahk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[1PL][DIR][3PL-OBJ][INCL]:@R.order.conj@@R.person.ki@@R.direction.aa@yaahkwik EndVerb ;

!! Conjunct Order Inverse
@R.order.conj@@R.person.ni@@R.direction.i@[3SG][INV][1SG-OBJ]:@R.order.conj@@R.person.ni@@R.direction.i@t EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.i@[3PL][INV][1SG-OBJ]:@R.order.conj@@R.person.ni@@R.direction.i@tik EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.ikw@[3SG][INV][1PL-OBJ][EXCL]:@R.order.conj@@R.person.ni@@R.direction.ikw@aahk EndVerb ;
@R.order.conj@@R.person.ni@@R.direction.ikw@[3PL][INV][1PL-OBJ][EXCL]:@R.order.conj@@R.person.ni@@R.direction.ikw@aahkwik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.ikw@[3SG][INV][1PL-OBJ][INCL]:@R.order.conj@@R.person.ki@@R.direction.ikw@aahk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.ikw@[3PL][INV][1PL-OBJ][INCL]:@R.order.conj@@R.person.ki@@R.direction.ikw@aahkwik EndVerb ;


!! Second Person Subject Independent Order Direct
@R.order.indep@@R.person.ki@@R.direction.i@[2SG][DIR][1SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.i@n EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.i@[2SG][DIR][1PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.i@naan EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[2SG][DIR][3SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.aa@w EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[2SG][DIR][3PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.aa@waak EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.i@[2PL][DIR][1-OBJ]:@R.order.indep@@R.person.ki@@R.direction.i@naawaaw EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[2PL][DIR][3SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.aa@waaw EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.aa@[2PL][DIR][3PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.aa@waawaak EndVerb ;

!! Independent Order Inverse
@R.order.indep@@R.person.ki@@R.direction.iti@[1SG][INV][2SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.it@in EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.iti@[1PL][INV][2SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.it@inaan EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3SG][INV][2SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.ikw@w EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3PL][INV][2SG-OBJ]:@R.order.indep@@R.person.ki@@R.direction.ikw@iwaak EndVerb ;
!! TODO: What's [1]? Is it supposed to be [1PL]?
@R.order.indep@@R.person.ki@@R.direction.iti@[1][INV][2PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.it@inawaaw EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3SG][INV][2PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.ikw@waaw EndVerb ;
@R.order.indep@@R.person.ki@@R.direction.ikw@[3PL][INV][2PL-OBJ]:@R.order.indep@@R.person.ki@@R.direction.ikw@waawaak EndVerb ;

!! Second Person Subject Conjunct Order Direct

@R.order.conj@@R.person.ki@@R.direction.i@[2SG][DIR][1SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@yen EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.i@[2SG][DIR][1PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@yaahk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[2SG][DIR][3SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.aa@t EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[2SG][DIR][3PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.aa@tik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[2PL][DIR][3SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.aa@yehk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.aa@[2PL][DIR][3PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.aa@yehkwik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.i@[2PL][DIR][1SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@yehk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.i@[2PL][DIR][1PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@yaahk EndVerb ;

!! Conjunt Order inverse
@R.order.conj@@R.person.ki@@R.direction.it@[1SG][INV][2SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.it@aan EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.it@[1PL][INV][2SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.it@aahk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.i@[3SG][INV][2SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@shk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.i@[3PL][INV][2SG-OBJ]:@R.order.conj@@R.person.ki@@R.direction.i@shkik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.ikw@[3SG][INV][2PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.ikw@ehk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.ikw@[3PL][INV][2PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.ikw@ehkwik EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.it@[1SG][INV][2PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.it@ehk EndVerb ;
@R.order.conj@@R.person.ki@@R.direction.it@[1PL][INV][2PL-OBJ]:@R.order.conj@@R.person.ki@@R.direction.it@aahk EndVerb ;


!! Third Person Subject Independent Order Direct
@R.order.indep@@D.person@@R.direction.ee@[3SG][DIR][3-OBJ]:@R.order.indep@@D.person@@R.direction.ee@w VTAObviative ;
@R.order.indep@@D.person@@R.direction.ee@[3PL][DIR][3-OBJ]:@R.order.indep@@D.person@@R.direction.ee@waak VTAObviative ;

!! Independent Order Inverse
@R.order.indep@@D.person@@R.direction.i@[3SG][INV][3-OBJ]:@R.order.indep@@D.person@@R.direction.i@k VTAObviative ;
@R.order.indep@@D.person@@R.direction.i@[3PL][INV][3-OBJ]:@R.order.indep@@D.person@@R.direction.i@waak VTAObviative ;

!! Third Person Subject Conjunct Order Direct
@R.order.conj@@D.person@@R.direction.aa@[3-SUBJ][DIR][3-OBJ]:@R.order.conj@@D.person@@R.direction.aa@t EndVerb ;
@R.order.conj@@D.person@@R.direction.aa@[3-SUBJ][DIR][3-OBJ]:@R.order.conj@@D.person@@R.direction.aa@tik EndVerb ;

LEXICON VAIObviative1
@D.obv@                EndVerb ; !! Not obviative, don't care
@R.obv@[OBV]:@R.obv@   VAIObviative2 ;

LEXICON VAIObviative2 !! Here, the word is DEFINITELY obviative!
@D.order.indep@:@D.order.indep@     EndVerb ;
@R.order.indep@:@R.order.indep@%<a  EndVerb ;

LEXICON VTAObviative

@R.obv@[OBV]:@R.obv@ EndVerb ;

LEXICON EndVerb
0 # ;
