Multichar_Symbols

!! Below are the multichar symbols relevant to Michif verbal morphology

[VAI]

[OBV]
[PRS]
[FUT]
[PST]
[INC]
[EXCL]

[1SG]
[2SG]
[3SGA]
[NON3SG]
[1PL]
[3PLA]

!! Flags for Michif verbal morphology

@P.order.indep@
@R.order.indep@
@D.order.indep@
@P.order.conj@
@R.order.conj@
@P.person.ni@
@R.person.ni@
@P.person.ki@
@R.person.ki@
@P.tense.kii@
@R.tense.kii@
@P.tense.ka@
@R.tense.ka@
@P.tense.ka@
@D.tense@
@P.obv.yi@
@R.obv.yi@
@P.obv.a@
@R.obv.a@
@R.obv@
@D.obv@

!! Groups of Michif verbs

!! VAI: Animate intransitive

LEXICON Root

0 Verb ;


LEXICON Verb

@P.order.indep@@P.person.ni@:@P.order.indep@@P.person.ni@ni%< TenseMarking ;
@P.order.indep@@P.person.ki@:@P.order.indep@@P.person.ki@ki%< TenseMarking ;

@P.order.conj@:@P.order.conj@ee-%< TenseMarking ; ! TODO: Ask Heather about hyphen

LEXICON TenseMarking

0 VAIStem ; ! No tense marking
@P.tense.kii@:@P.tense.kii@kii%< VAIStem ;
@P.tense.ka@:@P.tense.ka@ka%< VAIStem ;


LEXICON VAIStem

! ayaw        VAIInflection;
itohteew:itoht    VAIInflection;
! shipweeteew VAIInflection;
! itweew      VAIInflection;
! kiweew      VAIInflection;
! meekiw      VAIInflection;

! I	         nititohtaan	ee-itohteeyaan
! you (one)	 kititohtaan	ee-itohteeyan

!! Everything that happens DIRECTLY after the stem:
LEXICON VAIInflection

[VAI]:%> ObviativeMarking ;


LEXICON ObviativeMarking !! Accepts for the first obviation marking

0 VAIOrder ; ! No obviative marking
@P.obv.yi@:@P.obv.yi@yi%< VAIOrder;

LEXICON VAIOrder !! Outputs the order tag
@R.order.indep@[IND]:@R.order.indep@ Tense ;
@R.order.conj@[CONJ]:@R.order.conj@ Tense ;


LEXICON Tense   !! Outputs the tense tag
@D.tense@[PRS]:@D.tense@ VAIPersonMarking ;
@R.tense.kii@[PST]:@R.tense.kii@ VAIPersonMarking ;
@R.tense.ka@[FUT]:@R.tense.ka@ VAIPersonMarking ;

LEXICON VAIPersonMarking

@R.order.indep@@R.person.ni@[1SG]:@R.order.indep@@R.person.ni@aan VAIObviative1 ;
@R.order.indep@@R.person.ki@[2SG]:@R.order.indep@@R.person.ki@aan VAIObviative1 ;
@R.order.indep@[3SGA]:@R.order.indep@eew VAIObviative1 ;
@R.order.indep@[NON3SG]:@R.order.indep@een VAIObviative1 ;
@R.order.indep@[1PL]:@R.order.indep@eenaan VAIObviative1 ;
!@R.order.indep@[2PL]:@R.order.indep@ VAIObviative1 ;
@R.order.indep@[3PLA]:@R.order.indep@eewak VAIObviative1 ;

@R.order.conj@[1SG]:@R.order.conj@eeyaan VAIObviative1 ;
@R.order.conj@[2SG]:@R.order.conj@eeyan VAIObviative1 ;
@R.order.conj@[3SG]:@R.order.conj@eet VAIObviative1 ;

LEXICON VAIObviative1
@D.obv@                EndVerb ; !! Not obviative, don't care
@R.obv@[OBV]:@R.obv@   VAIObviative2 ;

LEXICON VAIObviative2 !! Here, the word is DEFINITELY obviative!
@D.order.indep@:@D.order.indep@     EndVerb ;
@R.order.indep@:@R.order.indep@%<a  EndVerb ;

LEXICON EndVerb
0 # ;
